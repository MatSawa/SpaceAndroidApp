plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion versions.sdkVersion
    buildToolsVersion versions.toolsVersion

    defaultConfig {
        applicationId "com.sawapps.space"

        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.sdkVersion
        versionCode versions.codeVersion
        versionName versions.applicationVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix '.debug'
        }
    }

    flavorDimensions 'environment'

    productFlavors {
        production {
            dimension 'environment'
            resValue "string", "app_name", "Space"
            buildConfigField 'String', 'API_URL', urls.productionApi
        }

        development {
            applicationIdSuffix '.dev'
            dimension 'environment'
            resValue "string", "app_name", "Dev Space"
            buildConfigField 'String', 'API_URL', urls.developmentApi
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        htmlReport true
        htmlOutput file("$project.buildDir/reports/lint/lint-result-app.html")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation "androidx.appcompat:appcompat:$versions.androidX"
    implementation "androidx.core:core-ktx:$versions.ktx"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"

    //Rx
    implementation "io.reactivex.rxjava3:rxandroid:$versions.rxjava"
    implementation "com.uber.autodispose2:autodispose:$versions.autodispose"
    implementation "com.uber.autodispose2:autodispose-android:$versions.autodispose"
    implementation "com.uber.autodispose2:autodispose-androidx-lifecycle:$versions.autodispose"
    implementation "com.uber.autodispose2:autodispose-lifecycle:$versions.autodispose"

    //UI
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.recyclerview:recyclerview:$versions.recyclerview"
    implementation "androidx.fragment:fragment-ktx:$versions.fragment"

    //Room
    implementation "androidx.room:room-runtime:$versions.room"
    implementation "androidx.room:room-rxjava2:$versions.room"
    kapt "androidx.room:room-compiler:$versions.room"

    //DI
    implementation "org.koin:koin-core:$versions.koin"
    implementation "org.koin:koin-core-ext:$versions.koin"
    implementation "org.koin:koin-android:$versions.koin"
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"
    implementation "org.koin:koin-androidx-ext:$versions.koin"

    //Network
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.moshi:moshi-kotlin:$versions.moshi"
    debugImplementation "com.readystatesoftware.chuck:library:$versions.chuck"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$versions.chuck"

    //Navigation
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"

    //Utils
    implementation "com.orhanobut:logger:$versions.logger"
    implementation "io.coil-kt:coil:$versions.coil"

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}